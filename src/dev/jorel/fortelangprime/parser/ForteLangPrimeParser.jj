options {
	STATIC = false;
}

PARSER_BEGIN(ForteLangPrimeParser)

package dev.jorel.fortelangprime.parser;

import java.io.ByteArrayInputStream;
import java.io.InputStream;
import java.nio.charset.Charset;
import java.util.ArrayList;
import java.util.List;

import dev.jorel.fortelangprime.ast.FLPFunction;
import dev.jorel.fortelangprime.ast.FLPLibrary;
import dev.jorel.fortelangprime.ast.expressions.Expr;
import dev.jorel.fortelangprime.ast.expressions.ExprBoolLit;
import dev.jorel.fortelangprime.ast.expressions.ExprIntLit;
import dev.jorel.fortelangprime.ast.expressions.ExprStringLit;
import dev.jorel.fortelangprime.ast.types.Type;

public class ForteLangPrimeParser {
	
	public static FLPLibrary parse(String input) throws ParseException {
		InputStream inputStream = new ByteArrayInputStream(input.getBytes(Charset.forName("UTF-8")));
		return new ForteLangPrimeParser(inputStream).input();
	}
	
}

PARSER_END(ForteLangPrimeParser)

//Symbols
TOKEN : { < OPENBRACKET : "(" > }
TOKEN : { < CLOSEBRACKET : ")" > }
TOKEN : { < OPENCBRACE : "{" > }
TOKEN : { < CLOSECBRACE : "}" > }
TOKEN : { < OPENSBRACKET : "[" > }
TOKEN : { < CLOSESBRACKET : "]" > }
TOKEN : { < COLON : ":" > }
TOKEN : { < SEMICOLON : ";" > }
TOKEN : { < ARROW : "->" > }
TOKEN : { < COMMA : "," > }
TOKEN : { < FAT_ARROW : "=>" > }
TOKEN : { < DOUBLE_ARROW : "->>" > }
TOKEN : { < PLAY_BUTTON : "|>" > }
TOKEN : { < AT : "@" > }
TOKEN : { < GUARD : "?:" > }
TOKEN : { < PIPE : "|" > }
TOKEN : { < EQUALS : "=" > }
TOKEN : { < LCHEVRON : "<" > }
TOKEN : { < RCHEVRON : ">" > }


//Operators
TOKEN : { < CONCAT : "++" > }
TOKEN : { < PLUS : "+" >}
TOKEN : { < ROUGHLY_EQUALS : "~=" > }

//Types
TOKEN : { < TYPE_INT : "Int" > }
TOKEN : { < TYPE_BOOL : "Bool" > }
TOKEN : { < TYPE_STRING : "String" > }
//TOKEN : { < LIST : "List" > } //We'll use the [] notation to denote a List
//TOKEN : { < SET : "Set" >} //We'll use the {} notation to denote a Set

//Keywords
TOKEN : { < TRUE : "true" > }
TOKEN : { < FALSE : "false" > }
TOKEN : { < IF : "if" > }
TOKEN : { < THEN : "then" > }
TOKEN : { < ELSE : "else" > }

//File declaractions
TOKEN : { < LIBRARY : "Library" > }
//TOKEN : { < SCRIPT : "Script" > }
//TOKEN : { < FILE_TYPE : < LIBRARY > | < SCRIPT > >  }
//TOKEN : { < IMPORT : "import" > }
TOKEN : { < EXPORT : "export" > }
TOKEN : { < AS : "as" > }


//Other
TOKEN : { < VAR_NAME : ["A"-"Z", "a"-"z", "_"] (["A"-"Z", "a"-"z", "0"-"9", "_"])* > }
TOKEN : { < INT_LITERAL : (["0"-"9"])+ > }
TOKEN : { < NUMBER : (["0"-"9"])+ (["."])? (["0"-"9"])* > }
TOKEN : { < STRING : "\"" ("\\" ~[] | ~["\"", "\\"])* "\"" >}

//Whitespace
SKIP : { "\n" | "\r" | " " | "\t" }
SKIP : { < "#" (~["\r", "\n"])* > }

/** Main endpoint */
FLPLibrary input() : { FLPLibrary lib; } {
	lib = program()
	<EOF>
	{ return lib; }
}

FLPLibrary program() : { Token name; List<String> exports; List<FLPFunction> functions; } {
  	< LIBRARY >
	name = < VAR_NAME >
	
	< OPENCBRACE >
	exports = exports()
	< CLOSECBRACE >
	
	functions = functions()
	{ return new FLPLibrary(name.image, exports, functions); }
}

List<String> exports() : { List<String> exports = new ArrayList<String>(); Token t; } {
	(
	  	< EXPORT >
	  	t = < VAR_NAME >
	  	< SEMICOLON >
	  	{ exports.add(t.image); }
	)*
	{ return exports; }
}

void epsilon() : { } {
	{
	  { }
	}
}

List<FLPFunction> functions() : {
  	List<FLPFunction> functions = new ArrayList<FLPFunction>();
  	FLPFunction f;
} {
  	< OPENCBRACE >
  	( 	  
		f = functionDeclaration()
		{ functions.add(f); }
	)*
	< CLOSECBRACE >
	{ return functions; }
} 

FLPFunction functionDeclaration() : {
  	Token name;
  	Expr expr;
} {
	name = < VAR_NAME >
	types()
	< EQUALS >
	expr = expression()
	< SEMICOLON >
	{ return new FLPFunction(name.image, new Type[0], null, expr); }
}

ExprIntLit integer() : {
  	Token t;
} {
  	t = < INT_LITERAL >
  	{ return new ExprIntLit(Integer.parseInt(t.image)); }	
}

Expr expression() : {
  	Expr expr;
} {
	expr = integer() { return expr; }
	| expr = bool() { return expr; }
	| expr = string() { return expr; }
}

ExprBoolLit bool() : {} {
	( < TRUE >  { return new ExprBoolLit(true); }
	| < FALSE > { return new ExprBoolLit(false); }
	)
}

ExprStringLit string() : {
  	Token t;
} {
  	t = < STRING >
	{ return new ExprStringLit(t.image.substring(1, t.image.length() - 1)); }
}

void types() : { } {
	(< VAR_NAME >)?
	< LCHEVRON >
	type()
	< RCHEVRON >
	(		< ARROW >
		types()
	)?
}

void type() : { } {
	< TYPE_INT >
	| < TYPE_STRING >
	| < TYPE_BOOL >
	| < OPENSBRACKET > type() < CLOSESBRACKET >
}
